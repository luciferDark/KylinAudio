apply plugin: 'com.android.library'
apply plugin: 'org.greenrobot.greendao'
apply plugin: 'maven'

android {
    compileSdkVersion rootProject.android.compileSdkVersion


    defaultConfig {
        minSdkVersion rootProject.android.minSdkVersion
        targetSdkVersion rootProject.android.targetSdkVersion
        versionCode rootProject.android.versionCode
        versionName rootProject.android.versionName
        multiDexEnabled rootProject.android.multiDexEnabled
        testInstrumentationRunner rootProject.android.testInstrumentationRunner

        //添加了javaCompileOptions
        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath = true
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation rootProject.depsLibs.appcompat_v7
    testImplementation rootProject.depsLibs.junit
    androidTestImplementation rootProject.depsLibs.runner
    androidTestImplementation rootProject.depsLibs.espresso_core

    implementation rootProject.depsLibs.eventbus
    implementation rootProject.depsLibs.greendao

    implementation project(path: ':lib_image_loader')
}

greendao {
    //数据库版本号
    schemaVersion 1
    //设置DaoMaster、DaoSession、Dao包名
    daoPackage 'com.ll.lib_audio.mediaplayer.greendao.gen'
    //设置DaoMaster、DaoSession、Dao目录
    targetGenDir 'src/main/java'
    //设置自动生成单元测试用例
    generateTests  false
}

def pomName = this.getName()
def pomVersion = POM_VERSION
def pomDescription = "this is the project :" + this.getName()
//添加项目maven库上传gradle构建命令
uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: rootProject.mavenConfig.nowMavenUrl) {
                authentication( userName: NEXUS_NAME,
                        password: NEXUS_PASSWORD)
            }
            //groupId: com.ll.android
            //artifactId: lib_xxx
            //version: POM_VERSION
            pom.project {
                name pomName
                version pomVersion
                description pomDescription
                artifactId pomName
                groupId POM_GROUP_ID
                packaging POM_PACKAGING
            }
        }
    }
}